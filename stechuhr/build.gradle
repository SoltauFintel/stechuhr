apply plugin: 'java'
apply plugin: 'eclipse'
//apply plugin: 'pmd'
//apply plugin: 'findbugs'

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.description = 'Stechuhr - einfache Stundenerfassung'
group = 'de.mwvb.stechuhr'
version = '0.2'

dependencies {
    compile 'de.mwvb:xmldocument:0.1.0'

	testCompile 'junit:junit:4.12'
}

repositories {
	mavenCentral()
}

// Mit diesem task wird die Anwendung gebaut. Man erhaelt dann eine doppelklickbare StandAlone-JAR.
task fatJar(type: Jar) {
	manifest {
        attributes 'Main-Class': 'de.mwvb.stechuhr.Application'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

apply plugin: 'jacoco'

jacoco {
	toolVersion = '0.7.1.201405082137'
}

jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled true
	}
	afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/gui/**']) /* Dateien aus den gui Packages kann
            man eh nicht gescheit testen, insbesondere nicht auf Travis CI. */
        })
    }
}

test {
	testLogging { // bessere Fehleranzeige beim Travis CI Build
		exceptionFormat = 'full'
		events 'failed', 'standardOut', 'standardError'
	}
	jacoco {
		append = false
		destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
		classDumpFile = file("$buildDir/jacoco/classpathdumps")
	}
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
 }
 